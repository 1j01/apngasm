CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT(APNGASM_CLI)

# Generate version header
SET(APNGASM_CLI_VERSION_MAJOR 0)
SET(APNGASM_CLI_VERSION_MINOR 1)
SET(APNGASM_CLI_VERSION_PATCH 1)
SET(APNGASM_CLI_VERSION "${APNGASM_CLI_VERSION_MAJOR}.${APNGASM_CLI_VERSION_MINOR}.${APNGASM_CLI_VERSION_PATCH}")

CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/src/apngasm-cli-version.h.in ${PROJECT_BINARY_DIR}/src/apngasm-cli-version.h @ONLY)
INCLUDE_DIRECTORIES(${PROJECT_BINARY_DIR}/src)

LINK_DIRECTORIES(${PROJECT_BINARY_DIR}/../lib)
INCLUDE_DIRECTORIES(
  ${PROJECT_SOURCE_DIR}/../lib/src
  ${PROJECT_BINARY_DIR}/../lib/src
)

ADD_EXECUTABLE(apngasm-cli
    ${PROJECT_SOURCE_DIR}/src/apngasm-cli.cpp
    ${PROJECT_SOURCE_DIR}/src/options.cpp
    ${PROJECT_SOURCE_DIR}/src/cli.cpp
)
SET_TARGET_PROPERTIES(apngasm-cli
	PROPERTIES OUTPUT_NAME apngasm)

TARGET_LINK_LIBRARIES(apngasm-cli apngasm)

FIND_PACKAGE(Boost REQUIRED COMPONENTS program_options regex filesystem system) 
INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIRS}) 
TARGET_LINK_LIBRARIES(apngasm-cli ${Boost_LIBRARIES})

INSTALL(TARGETS apngasm-cli
	RUNTIME	DESTINATION bin
	COMPONENT applications)

##CPack definitions for package creation
INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_PACKAGE_VERSION_MAJOR "${APNGASM_CLI_VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${APNGASM_CLI_VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${APNGASM_CLI_VERSION_PATCH}")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "apngasm APNG assembler command line tool")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${PROJECT_SOURCE_DIR}/DESCRIPTION.txt")
SET(CPACK_PACKAGE_LICENSE_FILE "${PROJECT_SOURCE_DIR}/LICENSE.txt")
SET(CPACK_PACKAGE_CONTACT "zero@genshin.org")
SET(CPACK_PACKAGE_FILE_NAME "apngasm_${APNGASM_CLI_VERSION}")
SET(CPACK_COMPONENTS applications)

IF(UNIX)
	SET(CPACK_GENERATOR "DEB")
	SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libapngasm (>= ${APNGASM_LIB_VERSION})")
ENDIF(UNIX)
IF(APPLE)
	SET(CPACK_GENERATOR "PackageMaker")
ENDIF(APPLE)
IF(WIN32)
	SET(CPACK_GENERATOR "NSIS")
ENDIF(WIN32)

INCLUDE(CPack)
